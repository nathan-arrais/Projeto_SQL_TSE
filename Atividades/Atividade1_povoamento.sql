USE SiriusTSE;
GO

-- Criação da tabela Temporaria para importação
IF EXISTS (SELECT 1 FROM sys.tables WHERE name = 'Temporaria')
DROP TABLE dbo.Temporaria;
GO

CREATE TABLE Temporaria (
    DT_GERACAO DATE,
    HH_GERACAO TIME,
    ANO_ELEICAO INT,
    CD_TIPO_ELEICAO INT,
    NM_TIPO_ELEICAO VARCHAR(100),
    NR_TURNO INT,
    CD_ELEICAO INT,  
    DS_ELEICAO VARCHAR(255),
    DT_ELEICAO VARCHAR(255),
    TP_ABRANGENCIA_ELEICAO VARCHAR(50),
    SG_UF CHAR(2),
    SG_UE VARCHAR(10),
    NM_UE VARCHAR(100),
    CD_CARGO INT, 
    DS_CARGO VARCHAR(100),
    SQ_CANDIDATO BIGINT, 
    NR_CANDIDATO VARCHAR(50) NOT NULL,  
    NM_CANDIDATO VARCHAR(255) NOT NULL,  
    NM_URNA_CANDIDATO VARCHAR(50) NOT NULL,  
    NM_SOCIAL_CANDIDATO VARCHAR(255),  
    NR_CPF_CANDIDATO VARCHAR(50), 
    DS_EMAIL VARCHAR(100),  
    CD_SITUACAO_CANDIDATURA INT,
    DS_SITUACAO_CANDIDATURA VARCHAR(100),
    TP_AGREMIACAO VARCHAR(255),
    NR_PARTIDO INT,
    SG_PARTIDO VARCHAR(50),  
    NM_PARTIDO VARCHAR(255),
    NR_FEDERACAO INT,
    NM_FEDERACAO VARCHAR(255),
    SG_FEDERACAO VARCHAR(255),
    DS_COMPOSICAO_FEDERACAO VARCHAR(255),
    SQ_COLIGACAO CHAR(50), 
    NM_COLIGACAO VARCHAR(255),
    DS_COMPOSICAO_COLIGACAO VARCHAR(255),
    SG_UF_NASCIMENTO VARCHAR(255),
    DT_NASCIMENTO DATE,  
    NR_TITULO_ELEITORAL_CANDIDATO CHAR(50),  
    CD_GENERO INT CHECK (CD_GENERO IN (2, 4)),  
    DS_GENERO VARCHAR(255),
    CD_GRAU_INSTRUCAO INT,  
    DS_GRAU_INSTRUCAO VARCHAR(100),
    CD_ESTADO_CIVIL INT,  
    DS_ESTADO_CIVIL VARCHAR(255),
    CD_COR_RACA INT,  
    DS_COR_RACA VARCHAR(255),
    CD_OCUPACAO INT,  
    DS_OCUPACAO VARCHAR(100),  
    CD_SIT_TOT_TURNO INT, 
    DS_SIT_TOT_TURNO VARCHAR(255),
    PRIMARY KEY (SQ_CANDIDATO, CD_ELEICAO, NR_TURNO)
);
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

DECLARE @nomeArquivo VARCHAR(255)
DECLARE @caminhoArquivo VARCHAR(255)
DECLARE @extensao VARCHAR(255)
DECLARE @comando VARCHAR(MAX)

SET @caminhoArquivo = 'C:\Users\natha\Documents\MBA\SQL\candidato\'

DECLARE cursor_arquivos CURSOR FOR
SELECT 'consulta_cand_2024_AL.csv' AS nomeArquivo
UNION ALL
SELECT 'consulta_cand_2024_AM.csv'
UNION ALL
SELECT 'consulta_cand_2024_AP.csv'
UNION ALL
SELECT 'consulta_cand_2024_BA.csv'
UNION ALL
SELECT 'consulta_cand_2024_CE.csv'
UNION ALL
SELECT 'consulta_cand_2024_ES.csv'
UNION ALL
SELECT 'consulta_cand_2024_GO.csv'
UNION ALL
SELECT 'consulta_cand_2024_MA.csv'
UNION ALL
SELECT 'consulta_cand_2024_MG.csv'
UNION ALL
SELECT 'consulta_cand_2024_MS.csv'
UNION ALL
SELECT 'consulta_cand_2024_MT.csv'
UNION ALL
SELECT 'consulta_cand_2024_PA.csv'
UNION ALL
SELECT 'consulta_cand_2024_PB.csv'
UNION ALL
SELECT 'consulta_cand_2024_PE.csv'
UNION ALL
SELECT 'consulta_cand_2024_PI.csv'
UNION ALL
SELECT 'consulta_cand_2024_PR.csv'
UNION ALL
SELECT 'consulta_cand_2024_RJ.csv'
UNION ALL
SELECT 'consulta_cand_2024_RN.csv'
UNION ALL
SELECT 'consulta_cand_2024_RO.csv'
UNION ALL
SELECT 'consulta_cand_2024_RS.csv'
UNION ALL
SELECT 'consulta_cand_2024_SC.csv'
UNION ALL
SELECT 'consulta_cand_2024_SP.csv'
UNION ALL
SELECT 'consulta_cand_2024_SE.csv'
UNION ALL
SELECT 'consulta_cand_2024_TO.csv'
UNION ALL
SELECT 'consulta_cand_2024_RR.csv'

OPEN cursor_arquivos

FETCH NEXT FROM cursor_arquivos INTO @nomeArquivo

WHILE @@FETCH_STATUS = 0
BEGIN
    SET @comando = '
    BULK INSERT Temporaria
    FROM ''' + @caminhoArquivo + @nomeArquivo +'''
    WITH (
        FORMAT = ''CSV'',
        FIELDQUOTE = ''"'', -- Se os campos estiverem entre aspas
        ROWTERMINATOR = ''\n'',
        FIRSTROW = 2, -- Ignora o cabeçalho, se houver
        FIELDTERMINATOR = '';'', -- Separador de campos
		CODEPAGE = ''1252'',
		TABLOCK
    );'

	PRINT ''
	PRINT 'Lendo o arquivo: ' + @nomeArquivo
    EXEC(@comando)
	
    FETCH NEXT FROM cursor_arquivos INTO @nomeArquivo
	
END

CLOSE cursor_arquivos
DEALLOCATE cursor_arquivos

-- Inserir dados nas tabelas finais

-- Tabela UF
INSERT INTO UF (SG_UF, SG_UE, NM_UE)
SELECT DISTINCT SG_UF, SG_UE, NM_UE
FROM Temporaria
WHERE SG_UF IS NOT NULL AND SG_UE IS NOT NULL AND NM_UE IS NOT NULL
ORDER BY SG_UF;

-- Tabela CARGO
INSERT INTO CARGO (CD_CARGO, DS_CARGO)
SELECT DISTINCT CD_CARGO, DS_CARGO
FROM Temporaria;

-- Tabela SITUACAO_CANDIDATURA
INSERT INTO SITUACAO_CANDIDATURA (CD_SITUACAO_CANDIDATURA, DS_SITUACAO_CANDIDATURA)
SELECT DISTINCT CD_SITUACAO_CANDIDATURA, DS_SITUACAO_CANDIDATURA
FROM Temporaria;

-- Tabela PARTIDO
INSERT INTO PARTIDO (NR_PARTIDO, SG_PARTIDO, NM_PARTIDO)
SELECT DISTINCT NR_PARTIDO, SG_PARTIDO, NM_PARTIDO
FROM Temporaria;

-- Tabela GRAU_INSTRUCAO
INSERT INTO GRAU_INSTRUCAO (CD_GRAU_INSTRUCAO, DS_GRAU_INSTRUCAO)
SELECT DISTINCT CD_GRAU_INSTRUCAO, DS_GRAU_INSTRUCAO
FROM Temporaria;


-- Tabela OCUPACAO
INSERT INTO OCUPACAO (CD_OCUPACAO, DS_OCUPACAO)
SELECT DISTINCT CD_OCUPACAO, DS_OCUPACAO
FROM Temporaria;

-- Tabela ELEICAO
INSERT INTO ELEICAO (CD_ELEICAO, ANO_ELEICAO, DT_ELEICAO, CD_TIPO_ELEICAO, NM_TIPO_ELEICAO, TP_ABRANGENCIA_ELEICAO)
SELECT DISTINCT CD_ELEICAO, ANO_ELEICAO, DT_ELEICAO, CD_TIPO_ELEICAO, NM_TIPO_ELEICAO, TP_ABRANGENCIA_ELEICAO
FROM Temporaria;

INSERT INTO CANDIDATO (
						SQ_CANDIDATO, 
						NR_TURNO, 
						NM_CANDIDATO, 
						NM_URNA_CANDIDATO, 
						NM_SOCIAL_CANDIDATO, 
						NR_CPF_CANDIDATO, 
						DS_EMAIL, 
						CD_GRAU_INSTRUCAO, 
						CD_OCUPACAO, SG_UF, 
						CD_CARGO, 
						CD_SITUACAO_CANDIDATURA, 
						CD_ELEICAO,
						NR_PARTIDO)
SELECT 
    SQ_CANDIDATO,
	NR_TURNO,
    NM_CANDIDATO,
    NM_URNA_CANDIDATO,
    NM_SOCIAL_CANDIDATO,
    NR_CPF_CANDIDATO,
    DS_EMAIL,
    CD_GRAU_INSTRUCAO,
    CD_OCUPACAO,
    SG_UF,
    CD_CARGO,
    CD_SITUACAO_CANDIDATURA,
    CD_ELEICAO,
	NR_PARTIDO
FROM Temporaria
WHERE NOT EXISTS (
    SELECT 1
    FROM CANDIDATO c
    WHERE c.SQ_CANDIDATO = Temporaria.SQ_CANDIDATO
    AND c.CD_ELEICAO = Temporaria.CD_ELEICAO
    AND c.NR_TURNO = Temporaria.NR_TURNO
);

DROP TABLE Temporaria;

